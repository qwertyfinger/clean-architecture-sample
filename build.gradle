/*
 * Copyright (c) 2019 Andrii Chubko
 */

buildscript {
  ext.kotlin_version = '1.3.21'
  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.0-rc02'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
  }
}

apply from: 'dependencies.gradle'

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }

  // increase maximum number of printed errors
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xmaxerrs" << "500"
    }
  }

  // force all support libraries to use the same version
  configurations.all {
    resolutionStrategy {
      eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.android.support') {
          details.useVersion "$supportLibraryVersion"
        }
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
